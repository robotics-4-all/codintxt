import communication
import utils
import types
import entity

VisualComponent:
    Gauge           |
    LogsDisplay     |
    ValueDisplay    |
    AliveDisplay    |
    JsonViewer      |
    PlotView        |
    Plot            |
    Button          |
    ButtonGroup
;


Gauge:
'Gauge' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('attribute:' attribute=NID)
    ('position:' position=Placement)
    ('minValue:' minValue=FLOAT)
    ('maxValue:' maxValue=FLOAT)
    ('leftColor:' leftColor=Color)?
    ('rightColor:' rightColor=Color)?
    ('levels:' levels=INT)?
    ('hideTxt:' hideTxt=BOOL)?
    ('unit:' unit=STRING)?
)#
'end'
;

LogsDisplay:
'LogsDisplay' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('attribute:' attribute=NID)
    ('maxMessages:' maxMsg=INT)?
    ('highlights:' '-' colorKeys*=Highlight['-'])?
    ('position:' position=Placement)
)#
'end'
;

ValueDisplay:
'ValueDisplay' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('attribute:' attribute=NID)
    ('position:' position=Placement)
    ('unit:' unit=STRING)?
)#
'end'
;

AliveDisplay:
'AliveDisplay' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('position:' position=Placement)
    ('timeout:' timeout=INT)?  // ms
)#
'end'
;

JsonViewer:
'JsonViewer' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('attribute:' attribute=NID)?
    ('position:' position=Placement)
)#
'end'
;

AlignType:
    'Center' |
    'Left' |
    'Right' |
    'Top' |
    'Bottom' |
    'Horizontal' |
    'Vertical'
;

Button:
'Button' name=ID
(
    ('label:' label=STRING)
    ('entity:' entity=[Entity:FQN|+m:entities*])
    ('dynamic:' dynamic=BOOL)?
    ('color:' color=Color)?
    ('background:' bg=Color)?
    ('hover:' hover=Color)?
    ('payload:' '-' payload*=Attribute['-'])
)#
'end'
;

ButtonGroup:
'ButtonGroup' name=ID
(
    ('label:' label=STRING)
    ('alignTxt:' alignTxt=AlignType)?
    ('alignBtns:' alignBtns=AlignType)?
    ('buttons:' '-' buttons*=[Button]['-'])
    ('position:' position=Placement)
)#
'end'
;

Plot:
'Plot' name=ID
(
    ('label:' label=STRING)
    ('topic:' topic=STRING)
    ('broker:' broker=[MessageBroker])
    ('ptype:' ptype=PlotType)
    ('attribute:' attribute=NID)
    ('color:' color=Color)?
    ('smooth:' smooth=BOOL)?
    ('verticalGrid:' verticalGrid=BOOL)?
    ('horizontalGrid:' horizontalGrid=BOOL)?
    ('xAxis:' xAxis=BOOL)?
    ('yAxis:' yAxis=BOOL)?
    ('legend:' legend=BOOL)?
    ('maxValues:' maxValues=INT)?
    ('legendPosition:' legendPosition=AlignType)?
)#
'end'
;

PlotView:
'PlotView' name=ID
(
    ('label:' label=STRING)
    ('position:' position=Placement)
    ('plots:' '-' plots*=[Plot]['-'])
)#
'end'
;

PlotType:
    'Line' |
    'Bar'
;

Highlight:
    'key:' key=STRING
    'color:' color=Color
;

// TODO
Color:
    'Red'		|
    'red'		|
    'Blue'		|
    'blue'		|
	'Yellow'	|
	'yellow'	|
	'Green'     |
	'green'     |
	'Cyan'      |
	'cyan'
;


Placement:
    'x:' x=INT
    'y:' y=INT
    'width:' w=INT
    'height:' h=INT
;
